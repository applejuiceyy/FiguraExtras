// NOT BUILDING: will be fixed when the official maven gets pre-5
// you would need to build it locally following some other steps

plugins {
    id 'fabric-loom' version '1.2-SNAPSHOT'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

repositories {

    maven { url = "https://maven.quiltmc.org/repository/release/" }
    maven { url 'https://jitpack.io' }
    maven { url 'https://maven.wispforest.io' }

    mavenLocal();
    // maven { url "https://maven.figuramc.org/releases" }


    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
    }
}

configurations.configureEach {
    resolutionStrategy.eachDependency {
        if (it.requested.module.name == "luaj-core" || it.requested.module.name == "luaj-jse") {
            it.useVersion "3.0.8-figura"
        }
    }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"

    mappings loom.layered {
        mappings("org.quiltmc:quilt-mappings:${project.minecraft_version}+build.1:intermediary-v2")
        officialMojangMappings()
    }

    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "org.figuramc:figura-fabric:${project.figura}+1.20.1"
    modImplementation "io.wispforest:owo-lib:0.11.1+1.20"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    include "io.wispforest:owo-sentinel:0.11.1+1.20"

    compileOnly("com.github.FiguraMC.LuaJ:luaj-core:3.0.6-figura")
    compileOnly("com.github.FiguraMC.LuaJ:luaj-jse:3.0.6-figura")
    implementation("com.neovisionaries:nv-websocket-client:2.14")
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version
    }
}

def targetJavaVersion = 17
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release = targetJavaVersion
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    archivesBaseName = project.archives_base_name
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}